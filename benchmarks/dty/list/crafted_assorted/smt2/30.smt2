(declare-datatypes ((Nat 0)) (((succ (pred Nat)) (zero))
))
(declare-datatypes ((Lst 0)) (((cons (head Nat) (tail Lst)) (nil))
))
(declare-fun leq (Nat Nat) Bool)
(assert (forall ((x Nat)) (leq x x)))
(assert (forall ((x Nat) (y Nat) (z Nat)) (=> (and (leq x y) (leq y z)) (leq x z))))
(assert (forall ((x Nat) (y Nat)) (=> (and (leq x y) (leq y x)) (= x y))))
(assert (forall ((x Nat) (y Nat)) (or (leq x y) (leq y x))))
(declare-fun append (Lst Lst) Lst)
(assert (forall ((x Lst)) (= (append nil x) x) ))
(assert (forall ((x Nat) (y Lst) (z Lst)) (= (append (cons x y) z) (cons x (append y z))) ))
(declare-fun rev (Lst) Lst)
(assert (= (rev nil) nil))
(assert (forall ((x Nat) (y Lst)) (= (rev (cons x y)) (append (rev y) (cons x nil))) ))
(declare-fun insort (Nat Lst) Lst)
(assert (forall ((i Nat)) (= (insort i nil) (cons i nil)) ))
(assert (forall ((i Nat) (x Nat) (y Lst)) (= (insort i (cons x y)) (ite (leq i x) (cons i (cons x y)) (cons x (insort i y)))) ))
(declare-fun sort (Lst) Lst)
(assert (= (sort nil) nil))
(assert (forall ((x Nat) (y Lst)) (= (sort (cons x y)) (insort x (sort y))) ))
(declare-fun rinsort (Nat Lst) Lst)
(assert (forall ((i Nat)) (= (rinsort i nil) (cons i nil)) ))
(assert (forall ((i Nat) (x Nat) (y Lst)) (= (rinsort i (cons x y)) (ite (not (leq i x)) (cons i (cons x y)) (cons x (rinsort i y)))) ))
(declare-fun rsort (Lst) Lst)
(assert (= (rsort nil) nil))
(assert (forall ((x Nat) (y Lst)) (= (rsort (cons x y)) (rinsort x (rsort y))) ))
(assert (not (forall ((xs Lst)) (= (rev (rsort xs)) (sort xs)) )))
(check-sat)
(exit)

