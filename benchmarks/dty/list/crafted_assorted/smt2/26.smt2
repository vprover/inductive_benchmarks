(declare-datatypes ((Nat 0)) (((succ (pred Nat)) (zero))
))
(declare-datatypes ((Lst 0)) (((cons (head Nat) (tail Lst)) (nil))
))
(declare-fun count (Nat Lst) Nat)
(assert (forall ((x Nat)) (= (count x nil) zero) ))
(assert (forall ((x Nat) (y Nat) (z Lst)) (= (count x (cons y z)) (ite (= x y) (succ (count x z)) (count x z))) ))
(declare-fun append (Lst Lst) Lst)
(assert (forall ((x Lst)) (= (append nil x) x) ))
(assert (forall ((x Nat) (y Lst) (z Lst)) (= (append (cons x y) z) (cons x (append y z))) ))
(declare-fun rotate (Nat Lst) Lst)
(assert (forall ((x Lst)) (= (rotate zero x) x) ))
(assert (forall ((n Nat)) (= (rotate (succ n) nil) nil) ))
(assert (forall ((n Nat) (y Nat) (x Lst)) (= (rotate (succ n) (cons y x)) (rotate n (append x (cons y nil)))) ))
(declare-fun shuffle (Lst Lst) Lst)
(assert (forall ((xs Lst)) (= (shuffle nil xs) nil)))
(assert (forall ((x Nat) (xs Lst) (ys Lst)) (= (shuffle (cons x xs) ys) (rotate x (shuffle xs ys))) ))
(assert (not (forall ((x Nat) (xs Lst) (ys Lst)) (= (count x (shuffle xs ys)) (count x ys)) )))
(check-sat)
(exit)

