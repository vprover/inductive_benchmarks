; TODO try replacing this with int
(declare-datatype nat ((zero) (s (s_0 nat))))
(declare-datatype list (par (a) ((Nil) (Cons (Cons_0 a) (Cons_1 (list a))))))
(declare-datatype heap (par (a) ((Empty) (Hp (Hp_0 a) (Hp_1 (list (heap a)))))))
(declare-fun plus (nat nat) nat)
(assert (forall ((x nat)) (= (plus zero x) x)))
(assert (forall ((x nat) (y nat)) (= (plus (s x) y) (s (plus x y)))))
(declare-fun less (par (a) (a a) Bool))
(declare-fun count_heap (par (a) (a (heap a)) nat))
(declare-fun count_heap_inner (par (a) (a (list (heap a))) nat))
(assert (par (a) (forall ((x a)) (= (count_heap_inner a x (Nil (heap a))) zero))))
(assert (par (a) (forall ((x a) (h (heap a)) (hs (list (heap a)))) (= (count_heap_inner a x (Cons (heap a) h hs))
  (plus (count_heap a x h) (count_heap_inner a x hs))))))
(assert (par (a) (forall ((x a)) (= (count_heap a x (Empty a)) zero))))
(assert (par (a) (forall ((x a) (y a) (ys (list (heap a)))) (= (count_heap a x (Hp a y ys))
  (ite (= x y) (s (count_heap_inner a x ys)) (count_heap_inner a x ys))))))
(declare-fun merge_heap (par (a) ((heap a) (heap a)) (heap a)))
(assert (par (a) (forall ((h (heap a))) (= (merge_heap a h (Empty a)) h))))
(assert (par (a) (forall ((h (heap a))) (= (merge_heap a (Empty a) h) h))))
(assert (par (a) (forall ((x a) (hsx (list (heap a))) (y a) (hsy (list (heap a)))) (= (merge_heap a (Hp a x hsx) (Hp a y hsy))
  (ite (less a x y) (Hp a x (Cons (heap a) (Hp a y hsy) hsx)) (Hp a y (Cons (heap a) (Hp a x hsx) hsy)))))))
(declare-fun get_min_heap (par (a) ((heap a)) a))
(assert (par (a) (forall ((x a) (hs (list (heap a)))) (= (get_min_heap a (Hp a x hs)) x))))

; mset_heap (merge h1 h2) = mset_heap h1 + mset_heap h2
(assert-not (par (a) (forall ((x a) (h1 (heap a)) (h2 (heap a))) (= (count_heap a x (merge_heap a h1 h2)) (plus (count_heap a x h1) (count_heap a x h2))))))
